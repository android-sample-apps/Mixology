import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlinx-serialization'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

static def buildTime() {
    def df = new SimpleDateFormat("dd_MM_yyyy") // you can change it
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

android {
    compileSdkVersion 30
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.yanivsos.mixological"
        minSdkVersion 25
        targetSdkVersion 30
        versionCode 16
        versionName "2.0.4"
        setProperty("archivesBaseName", "Mixological_v" + versionCode + "(" + versionName + ")_" + buildTime())
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //room export schemas
        // https://developer.android.com/training/data-storage/room/migrating-db-versions#export-schema
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true",
                                    analyticsCollectionEnabled  : "true"]
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false",
                                    analyticsCollectionEnabled  : "false"]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix '.debug'
            applicationIdSuffix '.debug'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Mixological_v${variant.versionCode}_${variant.versionName}_${buildTime()}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0'

    implementation 'androidx.core:core-ktx:1.6.0'

    implementation 'androidx.datastore:datastore-preferences:1.0.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0-alpha01'
    implementation 'androidx.startup:startup-runtime:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.3.0'

    // AndroidX
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.work:work-runtime-ktx:2.6.0-beta01'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.6.0'

    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'

    // Koin Dependency Injection
    implementation 'org.koin:koin-android:2.2.2'
    implementation 'org.koin:koin-android-scope:2.2.2'
    implementation 'org.koin:koin-android-viewmodel:2.2.2'

    // Room Database
    implementation 'androidx.room:room-runtime:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    implementation 'androidx.room:room-ktx:2.3.0'
    testImplementation 'androidx.room:room-testing:2.3.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.airbnb.android:lottie:3.6.0'
    implementation 'com.job:droidnet:2.0.0'

    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    debugImplementation 'com.balsikandar.android:crashreporter:1.1.0'
    implementation platform('com.google.firebase:firebase-bom:27.0.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    //noinspection GradleDependency
    implementation 'com.github.nikartm:image-support:1.0.5'

    implementation 'com.google.android.play:core:1.10.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    implementation 'com.github.Zhuinden:fragmentviewbindingdelegate-kt:1.0.0'

    implementation "com.xwray:groupie:2.9.0"
    implementation "com.xwray:groupie-viewbinding:2.9.0"

}
